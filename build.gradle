plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.5.31'
    // id 'checkstyle'

    // Fixes problem with long filenames
    id "ua.eshepelyuk.ManifestClasspath" version "1.0.0"

}

compileJava.options.encoding = 'UTF-8'

def versionName = "Simbrain4Beta"
def jmonkeyengine_version = '3.3.2-stable'

mainClassName = 'org.simbrain.workspace.gui.Splasher'

// When updating also search for "jvmTarget" and update those
sourceCompatibility = 1.11

repositories {
    mavenCentral()
    maven {
        url  "https://kotlin.bintray.com/kotlin-datascience"
    }
}

dependencies {

    // Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.5.21'
    implementation 'org.jetbrains.kotlin:kotlin-scripting-jvm-host:1.5.21'
    implementation 'org.jetbrains.kotlin:kotlin-scripting-jsr223:1.5.21'
    implementation 'org.jetbrains.kotlin:kotlin-compiler-embeddable:1.5.21'
    implementation 'org.jetbrains.kotlin:kotlin-scripting-compiler-embeddable:1.5.21'
    implementation 'org.jetbrains.kotlin:kotlin-script-util:1.5.21'
    implementation 'org.jetbrains.kotlin:kotlin-script-runtime:1.5.21'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1-native-mt'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-swing:1.5.1-native-mt'

    // Kotlin DL
    implementation 'org.jetbrains.kotlinx:kotlin-deeplearning-api:0.2.0'

    // Smile
    implementation 'com.github.haifengl:smile-kotlin:2.6.0'
    implementation 'com.github.haifengl:smile-mkl:2.6.0'

    // JUnit
    implementation 'org.junit.jupiter:junit-jupiter:5.7.2'

    testImplementation(platform('org.junit:junit-bom:5.7.1'))
    testImplementation('org.junit.jupiter:junit-jupiter:5.7.2')

    // https://mvnrepository.com/artifact/com.github.haifengl/smile-core
    implementation group: 'com.github.haifengl', name: 'smile-core', version: '2.6.0'

    // https://mvnrepository.com/artifact/ca.umontreal.iro.simul/ssj
    implementation group: 'ca.umontreal.iro.simul', name: 'ssj', version: '3.3.1'

    //https://mvnrepository.com/artifact/org.ostermiller/utils
    implementation group: 'org.ostermiller', name: 'utils', version: '1.07.00'

    // https://mvnrepository.com/artifact/org.tinylog/tinylog/1.3.6
    implementation group: 'org.tinylog', name: 'tinylog', version: '1.3.6'
    // https://mvnrepository.com/artifact/org.tinylog/tinylog-impl
    runtimeOnly group: 'org.tinylog', name: 'tinylog-impl', version: '2.4.0-M1'
    // https://mvnrepository.com/artifact/org.tinylog/slf4j-tinylog
    runtimeOnly group: 'org.tinylog', name: 'slf4j-tinylog', version: '2.4.0-M1'

    // https://mvnrepository.com/artifact/com.thoughtworks.xstream/xstream
    implementation group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.14'

    // https://mvnrepository.com/artifact/org.piccolo2d/piccolo2d-extras
    implementation group: 'org.piccolo2d', name: 'piccolo2d-extras', version: '3.0.1'
    implementation group: 'org.piccolo2d', name: 'piccolo2d-core', version: '3.0.1'

    // https://mvnrepository.com/artifact/jfree/jfreechart
    implementation group: 'jfree', name: 'jfreechart', version: '1.0.13'

    // https://mvnrepository.com/artifact/org.swinglabs/swingx-core
    implementation group: 'org.swinglabs', name: 'swingx-core', version: '1.6.2-2'

    // https://mvnrepository.com/artifact/com.miglayout/miglayout-swing
    implementation group: 'com.miglayout', name: 'miglayout-swing', version: '11.0'

    // https://mvnrepository.com/artifact/com.fifesoft/rsyntaxtextarea
    implementation group: 'com.fifesoft', name: 'rsyntaxtextarea', version: '3.1.3'

    // https://mvnrepository.com/artifact/org.beanshell/bsh
    implementation group: 'org.beanshell', name: 'bsh', version: '2.0b5'

    // https://mvnrepository.com/artifact/org.jmonkeyengine
    implementation "org.jmonkeyengine:jme3-core:$jmonkeyengine_version"
    implementation "org.jmonkeyengine:jme3-desktop:$jmonkeyengine_version"
    implementation "org.jmonkeyengine:jme3-jogl:$jmonkeyengine_version"
    implementation "org.jmonkeyengine:jme3-lwjgl:$jmonkeyengine_version"
    implementation "org.jmonkeyengine:jme3-bullet:$jmonkeyengine_version"
    implementation "org.jmonkeyengine:jme3-bullet-native:$jmonkeyengine_version"
    implementation "org.jmonkeyengine:jme3-plugins:$jmonkeyengine_version"

}

test {
    useJUnitPlatform()
}

// Sample invocation:
// gradle runSim -PsimName="Test Sim"
task runSim(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = "org.simbrain.custom_sims.RegisteredSimulationsKt"
    if(project.hasProperty('simName')){
        args simName
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
    kotlinOptions.freeCompilerArgs += "-Xuse-experimental=kotlin.experimental.ExperimentalTypeInference"
    kotlinOptions.useIR = true
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
    kotlinOptions.freeCompilerArgs += "-Xuse-experimental=kotlin.experimental.ExperimentalTypeInference"
    kotlinOptions.useIR = true
}

