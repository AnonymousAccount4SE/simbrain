plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.3.60'
    // id 'checkstyle'

    // Fixes problem with long filenames
    id "ua.eshepelyuk.ManifestClasspath" version "1.0.0"

    // Build executable jar using <code>gradle shadowJar</code>. The result is placed in be in build/libs
    id 'com.github.johnrengelman.shadow' version '5.2.0'

}


compileJava.options.encoding = 'UTF-8'

def versionName = "Simbrain4Beta"
def jmonkeyengine_version = '3.2.2-stable'
def dl4j_version = "1.0.0-beta4" // beta5 not working on mac

mainClassName = 'org.simbrain.workspace.gui.Splasher'

sourceCompatibility = 1.10

repositories {
    mavenCentral()
    jcenter()
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2'

    // https://mvnrepository.com/artifact/ca.umontreal.iro.simul/ssj
    compile group: 'ca.umontreal.iro.simul', name: 'ssj', version: '3.3.1'

    //https://mvnrepository.com/artifact/org.ostermiller/utils
    compile group: 'org.ostermiller', name: 'utils', version: '1.07.00'

    // https://mvnrepository.com/artifact/org.ojalgo/ojalgo
    compile group: 'org.ojalgo', name: 'ojalgo', version: '37.1.1'

    // https://mvnrepository.com/artifact/org.tinylog/tinylog/1.3.6
    compile group: 'org.tinylog', name: 'tinylog', version: '1.3.6'

    // https://mvnrepository.com/artifact/junit/junit
    compile "junit:junit:4.12"

    // https://mvnrepository.com/artifact/com.thoughtworks.xstream/xstream
    compile group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.10'

    // https://mvnrepository.com/artifact/org.piccolo2d/piccolo2d-extras
    compile group: 'org.piccolo2d', name: 'piccolo2d-extras', version: '3.0.1'
    compile group: 'org.piccolo2d', name: 'piccolo2d-core', version: '3.0.1'

    // https://mvnrepository.com/artifact/jfree/jfreechart
    compile group: 'jfree', name: 'jfreechart', version: '1.0.12'

    // https://mvnrepository.com/artifact/gov.nist.math/jama
    compile group: 'gov.nist.math', name: 'jama', version: '1.0.2'

    // https://mvnrepository.com/artifact/org.deeplearning4j/deeplearning4j-core
    compile "org.deeplearning4j:deeplearning4j-core:${dl4j_version}"
    compile "org.nd4j:nd4j-native-platform:${dl4j_version}"

    // https://mvnrepository.com/artifact/org.swinglabs/swingx-core
    compile group: 'org.swinglabs', name: 'swingx-core', version: '1.6.2-2'

    // https://mvnrepository.com/artifact/com.miglayout/miglayout-swing
    compile group: 'com.miglayout', name: 'miglayout-swing', version: '5.0'

    // https://mvnrepository.com/artifact/com.fifesoft/rsyntaxtextarea
    compile group: 'com.fifesoft', name: 'rsyntaxtextarea', version: '2.5.3'

    // https://mvnrepository.com/artifact/org.beanshell/bsh
    compile group: 'org.beanshell', name: 'bsh', version: '2.0b5'

    // https://mvnrepository.com/artifact/org.jmonkeyengine
    compile "org.jmonkeyengine:jme3-core:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-desktop:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-jogl:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-lwjgl:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-bullet:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-bullet-native:$jmonkeyengine_version"
    compile "org.jmonkeyengine:jme3-plugins:$jmonkeyengine_version"

}

// Sample call: gradle runSim -PsimName='Evolve Mouse Pursuer'
task runSim(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = "org.simbrain.custom_sims.RegisteredSimulation"
    if(project.hasProperty('simName')){
        args simName
    }
}

task printStuff {
    group 'Misc'
    description 'For printing variable bindings / learning gradle'
    doLast {
        println gradle.gradleHomeDir
        println mainClassName
        println project.buildDir
        println "" + project.buildDir + "/" + project.executableDir
        println rootProject
        println projectDir
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs += "-Xuse-experimental=kotlin.experimental.ExperimentalTypeInference"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

// Uncomment to minimize the JAR.  Currently only reduces < 10%, and the jar does not execute.
//shadowJar {
//    minimize()
//}
