plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
    // id 'checkstyle'
    // Fixes problem with long filenames
    id "ua.eshepelyuk.ManifestClasspath" version "1.0.0"
}

def versionName = "Simbrain4Beta"
def simbrainJvmArgs = [ '--add-opens','java.base/java.util=ALL-UNNAMED',
                        '--add-opens','java.desktop/java.awt=ALL-UNNAMED',
                        '--add-opens','java.desktop/java.awt.geom=ALL-UNNAMED',
                        '--add-opens', 'java.base/java.util.concurrent=ALL-UNNAMED',
                        '--add-opens', 'java.base/java.util.concurrent.atomic=ALL-UNNAMED',
                        '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
//                        '-Djava.library.path', '/opt/homebrew/opt/openblas/lib/',
//                        '-Dorg.bytedeco.openblas.load', 'blas'
]
mainClassName = 'org.simbrain.workspace.gui.Splasher'
compileJava.options.encoding = 'UTF-8'

// When updating also search for "jvmTarget" and update those
sourceCompatibility = 1.17

repositories {
    mavenCentral()
}

dependencies {

    // Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.7.20'
    implementation 'org.jetbrains.kotlin:kotlin-scripting-jvm-host:1.7.20'
    implementation 'org.jetbrains.kotlin:kotlin-scripting-jsr223:1.7.20'
    implementation 'org.jetbrains.kotlin:kotlin-compiler-embeddable:1.7.20'
    implementation 'org.jetbrains.kotlin:kotlin-scripting-compiler-embeddable:1.7.20'
    implementation 'org.jetbrains.kotlin:kotlin-script-util:1.7.20'
    implementation 'org.jetbrains.kotlin:kotlin-script-runtime:1.7.20'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-swing:1.6.4'

    // Kotlin DL
    implementation 'org.jetbrains.kotlinx:kotlin-deeplearning-api:0.4.0'

    // Smile
    implementation group: 'com.github.haifengl', name: 'smile-core', version: '3.0.1'
    implementation 'com.github.haifengl:smile-kotlin:3.0.1'
    implementation 'com.github.haifengl:smile-plot:3.0.1'
    implementation 'com.github.haifengl:smile-nlp:3.0.1'
    implementation group: 'org.bytedeco', name: 'openblas-platform', version: '0.3.21-1.5.8'

    // JUnit
    implementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation(platform('org.junit:junit-bom:5.7.1'))
    testImplementation('org.junit.jupiter:junit-jupiter:5.9.0')

    // https://mvnrepository.com/artifact/org.apache.commons/commons-math3
    implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'

    // jsoup HTML parser library @ https://jsoup.org/
    implementation 'org.jsoup:jsoup:1.15.3'

    //https://mvnrepository.com/artifact/org.ostermiller/utils
    implementation group: 'org.ostermiller', name: 'utils', version: '1.07.00'

    // https://mvnrepository.com/artifact/org.tinylog/tinylog/1.3.6
    implementation group: 'org.tinylog', name: 'tinylog', version: '1.3.6'
    // https://mvnrepository.com/artifact/org.tinylog/tinylog-impl
    runtimeOnly group: 'org.tinylog', name: 'tinylog-impl', version: '2.5.0'
    // https://mvnrepository.com/artifact/org.tinylog/slf4j-tinylog
    runtimeOnly group: 'org.tinylog', name: 'slf4j-tinylog', version: '2.5.0'

    // https://mvnrepository.com/artifact/com.thoughtworks.xstream/xstream
    implementation group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.19'

    // https://mvnrepository.com/artifact/org.piccolo2d/piccolo2d-extras
    implementation group: 'org.piccolo2d', name: 'piccolo2d-extras', version: '3.0.1'
    implementation group: 'org.piccolo2d', name: 'piccolo2d-core', version: '3.0.1'

    // https://mvnrepository.com/artifact/org.jfree/jfreechart
    implementation group: 'org.jfree', name: 'jfreechart', version: '1.5.3'

    // https://mvnrepository.com/artifact/org.swinglabs/swingx-core
    implementation group: 'org.swinglabs', name: 'swingx-core', version: '1.6.2-2'

    // https://mvnrepository.com/artifact/com.miglayout/miglayout-swing
    implementation group: 'com.miglayout', name: 'miglayout-swing', version: '11.0'

    // https://mvnrepository.com/artifact/com.fifesoft/rsyntaxtextarea
    implementation group: 'com.fifesoft', name: 'rsyntaxtextarea', version: '3.3.0'

    // https://mvnrepository.com/artifact/org.beanshell/bsh
    implementation group: 'org.beanshell', name: 'bsh', version: '2.0b5'

    // JMonkeyEngine
    implementation 'org.jmonkeyengine:jme3-core:3.5.2-stable'
    implementation 'org.jmonkeyengine:jme3-jbullet:3.5.2-stable'
    implementation 'org.jmonkeyengine:jme3-desktop:3.5.2-stable'
    implementation 'org.jmonkeyengine:jme3-lwjgl3:3.5.2-stable'

}

application {
    applicationDefaultJvmArgs = simbrainJvmArgs
}

test {
    jvmArgs simbrainJvmArgs
    useJUnitPlatform()
}

// Sample invocation:
// gradle runSim -PsimName="Test Sim"
task runSim(type: JavaExec) {
    jvmArgs simbrainJvmArgs
    classpath = sourceSets.main.runtimeClasspath
    main = "org.simbrain.custom_sims.RegisteredSimulationsKt"
    if(project.hasProperty('simName')){
        args simName
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "17"
    kotlinOptions.freeCompilerArgs += "-Xuse-experimental=kotlin.experimental.ExperimentalTypeInference"
    kotlinOptions.freeCompilerArgs += "-Xjvm-default=all"
    kotlinOptions.freeCompilerArgs += "-Xcontext-receivers"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "17"
    kotlinOptions.freeCompilerArgs += "-Xuse-experimental=kotlin.experimental.ExperimentalTypeInference"
    kotlinOptions.freeCompilerArgs += "-Xjvm-default=all"
    kotlinOptions.freeCompilerArgs += "-Xcontext-receivers"
}

